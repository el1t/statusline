#
# A two-line, responsive theme styled after powerline.
# Originally ported from agnoster, in oh-my-zsh (https://github.com/robbyrussell/oh-my-zsh)
# Async support from sorin's prompt in prezto
#
# Authors:
#   Ellis Tsung (github.com/el1t)
#

#
# ==============Colors==============
# Norm |  Color  | Bold | Solarized
# ----------------------------------
#   0  | Black   |   8  |  Base03
#   1  | Red     |   9  |  Orange
#   2  | Green   |  10  |  Base01
#   3  | Yellow  |  11  |  Base00
#   4  | Blue    |  12  |  Base0
#   5  | Magenta |  13  |  Violet
#   6  | Cyan    |  14  |  Base1
#   7  | White   |  15  |  Base3
#

# Load dependencies
pmodload 'helper'

# Global vars/constants
_prompt_statusline_length=0
_prompt_statusline_git=''
_prompt_statusline_bg=(7 15 7 '') # foreground, background, middle, current
_prompt_statusline_symbols=(   )
_prompt_statusline_left_segments=(status user git-branch directory)
_prompt_statusline_right_segments=(git-status clock history machine)

#
# Segment drawing utility
#

function prompt_statusline_draw {
	local -a left right start end center diamond standalone uncalculated
	local escape_sequences='%([BSUbfksu]|([FB]|){*})'
	zparseopts -D l=left -left=left \
		r=right -right=right \
		s=start -start=start \
		e=end -end=end \
		c=center -center=center \
		d=diamond -diamond=diamond \
		a=standalone -standalond=standalone \
		n=uncalculated -no-calc=uncalculated

	# Begin a left segment
	if [[ -n $left ]]; then
		local bg fg
		[[ -n $1 ]] && bg="%K{$1}" || bg='%k'
		[[ -n $2 ]] && fg="%F{$2}" || fg='%f'
		if [[ -n $_prompt_statusline_bg[4] ]]; then
			if [[ $1 != $_prompt_statusline_bg[4] ]]; then
				echo -n " $bg%F{$_prompt_statusline_bg[4]}$_prompt_statusline_symbols[4] "
			else
				echo -n " $bg%F{$_prompt_statusline_bg[2]}$_prompt_statusline_symbols[3] "
			fi
			[[ -z $uncalculated ]] && (( _prompt_statusline_length += 2 ))
		else
			echo -n "$bg "
		fi
		_prompt_statusline_bg[4]=$1
		echo -n $fg
		if [[ -n $3 ]]; then
			echo -n $3
			[[ -z $uncalculated ]] && (( _prompt_statusline_length += ${#${(S%%)${3}//$~escape_sequences/}} + 2 ))
		fi

	# Begin a right segment
	elif [[ -n $right ]]; then
		local bg fg
		[[ -n $1 ]] && bg="%K{$1}" || bg='%k'
		[[ -n $2 ]] && fg="%F{$2}" || fg='%f'
		if [[ -n $_prompt_statusline_bg[4] ]]; then
			if [[ $1 != $_prompt_statusline_bg[4] ]]; then
				echo -n "%F{$_prompt_statusline_bg[4]}$bg%S$_prompt_statusline_symbols[1]%s$fg "
			else
				echo -n "$bg%F{$_prompt_statusline_bg[2]}$_prompt_statusline_symbols[2]$fg "
			fi
			[[ -z $uncalculated ]] && (( _prompt_statusline_length += 2 ))
		else
			echo -n "%F{$1}$_prompt_statusline_symbols[1]$bg$fg "
		fi
		_prompt_statusline_bg[4]=$1
		if [[ -n $3 ]]; then
			echo -n "$3 "
			[[ -z $uncalculated ]] && (( _prompt_statusline_length += ${#${(S%%)${3}//$~escape_sequences/}} + 2 ))
		fi

	# Start the right prompt, setting the current bg color
	elif [[ -n $start ]]; then
		local bg
		[[ -n $_prompt_statusline_bg[3] ]] && bg="%K{$_prompt_statusline_bg[3]}" || bg='%k'
		echo -n "$bg"
		_prompt_statusline_bg[4]=$_prompt_statusline_bg[3]

	# End the left prompt, closing any open segments
	elif [[ -n $end ]]; then
		local bg
		[[ -n $_prompt_statusline_bg[3] ]] && bg="%K{$_prompt_statusline_bg[3]}" || bg='%k'
		if [[ -n $_prompt_statusline_bg[4] ]]; then
			echo -n " %F{$_prompt_statusline_bg[4]}$bg$_prompt_statusline_symbols[4]"
			[[ -z $uncalculated ]] && (( _prompt_statusline_length += 2 ))
		else
			echo -n $_prompt_statusline_bg[3]
		fi
		echo -n '%f'

	# Center a prompt
	elif [[ -n $center ]]; then
		local length=$(( (COLUMNS - $#3 - 4) / 2 ))
		[[ -n $4 ]] && echo -n "%K{$4}"
		# Pad spaces
		echo -n "${(l:$length:: :)}"
	fi

	#  Sample output 
	if [[ -n $diamond ]]; then
		local padding_color=$4
		[[ -z $4 ]] && padding_color=$_prompt_statusline_bg[2]
		echo -n "%F{$1}%K{$padding_color}$_prompt_statusline_symbols[1]%F{$2}%K{$1} $3 %F{$1}%K{$padding_color}$_prompt_statusline_symbols[4]%f%E%k"

	# Standalone
	elif [[ -n $standalone ]]; then
		local bg fg
		[[ -n $1 ]] && bg="%K{$1}" || bg="%K{$_prompt_statusline_bg[1]}"
		[[ -n $2 ]] && fg="%F{$2}" || fg='%f'
		echo -n "$fg$bg $3 %F{$_prompt_statusline_bg[2]}%S$_prompt_statusline_symbols[4]%s%f%k "
	fi
}

#
# Prompt components
#

function prompt_statusline_segments {
	local -a left right uncalculated draw_options
	zparseopts -D r=right -right=right \
		l=left -left=left \
		n=uncalculated -no-calc=uncalculated
	if [[ -z "$left$right" ]]; then
		echo "$0: Please specify an option" >&2
	fi

	# Start right prompt
	if [[ -n $right ]]; then
		draw_options=('-r')
	else
		draw_options=('-l')
	fi
	if [[ -n $uncalculated ]]; then
		draw_options+=('-n')
	fi

	while (( # > 0 )); do
		case "$1" in
			# Status: root/error/background jobs
			(status)
				local symbols=''
				[[ $UID == 0 || $EUID == 0 ]] && symbols+='%F{3}⚡'
				(( retval != 0 )) && symbols+='%F{1}✘'
				[[ -n $(jobs -l) ]] && symbols+='%F{6}⚙'

				if [[ -n $symbols ]]; then
					prompt_statusline_draw $draw_options 0 '' $symbols
				fi
				;;

			# User: username
			(user)
				if [[ -n "$SSH_CLIENT$SSH_TTY" ]]; then
					prompt_statusline_draw $draw_options 0 3 $LOGNAME
				fi
				;;

			# Machine: machine name
			(machine)
				if [[ -n "$SSH_CLIENT$SSH_TTY" ]]; then
					prompt_statusline_draw $draw_options 3 0 '@%m'
				fi
				;;

			# Git: branch, dirty status, commits behind/ahead of remote
			(git-branch)
				if [[ -n $_prompt_statusline_git ]]; then
					# If async has finished
					if (( _prompt_statusline_precmd_async_pid == 0 )); then
						# Declaring this inside the if statement would not be local
						local git_output="${(e)git_info[prompt]}"
						if [[ -n $git_output ]]; then
							prompt_statusline_draw -l $git_info[bg] 7 $git_output
						fi
					else
						# Display placeholder for async, gray color
						prompt_statusline_draw -l 14 0 ' ...   '
					fi
				fi
				;;

			# Git: format action, output other statuses defined in git_info[rprompt]
			(git-status)
				# If async has finished
				if [[ -n $_prompt_statusline_git && $_prompt_statusline_precmd_async_pid -eq 0 ]]; then
					if [[ -n $git_info[rprompt] ]]; then
						case $git_info[action] in
							(apply)
								prompt_statusline_draw -r $_prompt_statusline_bg[1] '' '<A<'
								;;
							(bisect)
								prompt_statusline_draw -r $_prompt_statusline_bg[1] '' '<B>'
								;;
							(cherry-pick|cherry-pick-sequence)
								prompt_statusline_draw -r $_prompt_statusline_bg[1] '' '<C<'
								;;
							(merge)
								prompt_statusline_draw -r $_prompt_statusline_bg[1] '' '>M<'
								;;
							(rebase|rebase-interactive|rebase-merge)
								prompt_statusline_draw -r $_prompt_statusline_bg[1] '' '>R>'
								;;
						esac
						prompt_statusline_draw $draw_options $_prompt_statusline_bg[1] '' $git_info[rprompt]
					fi
				fi
				;;

			# Dir: current working directory, shortens if longer than available space
			(directory)
				local directory="${${(%):-%~}}"
				local hashed_directory="${${directory[2,-1]%%/*}:-$directory[1]}"
				prompt_statusline_draw $draw_options 4 7 $hashed_directory
				# Replace directory with empty string if at base of hashed directory, else eliminate hashed portion
				directory="${${directory:#^?*/*}:+${directory#?*/}}"
				if [[ -n $directory ]]; then
					# Count spaces available for printing the working directory
					# Subtract the extra 3 chars used for the current segment and an extra space in case
					local spacing=$(( COLUMNS - _prompt_statusline_length - 3 - 1 ))
					local length="$#directory"
					if (( length <= spacing )); then
						prompt_statusline_draw $draw_options -n 4 7 $directory
					else
						# Shorten path as much as necessary
						local dir_segment
						prompt_statusline_draw -n $draw_options 4 7
						for dir_segment in "${(@s:/:)directory:h}"; do
							if (( length > spacing )); then
								echo -n "$dir_segment[1]/"
								(( length -= $#dir_segment - 1 ))
							else
								echo -n "$dir_segment/"
							fi
						done
						echo -n "${directory:t}"
					fi
				fi
				;;

			(clock)
				prompt_statusline_draw $draw_options 5 7 "${${(%%):-%t}## }"
				;;

			(history)
				prompt_statusline_draw $draw_options $_prompt_statusline_bg[1] '' '!%F{4}%!'
				;;

			(*)
				echo "$0: $1 is not a valid segment!" >&2
				;;
		esac
		shift
	done
}

# Output runtime of previous task
function prompt_statusline_elapsed_time {
	if [[ -n $_prompt_statusline_start_time ]]; then
		local end_time=$(( SECONDS - _prompt_statusline_start_time ))
		if (( end_time >= 10 )); then
			local -a output
			if (( end_time >= 60 )); then
				if (( end_time >= 3600 )); then
					output+=("$(( end_time / 3600 ))h")
					end_time=$(( end_time % 3600 ))
				fi
				output+=("$(( end_time / 60 ))m")
				end_time=$(( end_time % 60 ))
			fi
			output+=("${end_time}s")
			echo
			print -P '$(prompt_statusline_draw -cd 13 7 "$output" $_prompt_statusline_bg[1])'
		fi
		unset _prompt_statusline_start_time
	fi
}

function prompt_statusline {
	local retval=$?
	local -a left right single
	zparseopts l=left -left=left \
		r=right -right=right \
		s=single -single=single

	if [[ -n "$left" ]]; then
		[[ -z "$single" && -n "$TMUX" ]] && echo -n "%K{$_prompt_statusline_bg[1]}${(l:$COLUMNS:: :)}%k%{$(echotc LEFT $COLUMNS)%}"
		prompt_statusline_segments -r $_prompt_statusline_right_segments > /dev/null 2>&1
		_prompt_statusline_bg[4]=''
		prompt_statusline_segments -l $_prompt_statusline_left_segments
		prompt_statusline_draw -e
		[[ -z "$TMUX$single" ]] && echo -n '%E'

	elif [[ -n $right ]]; then
		prompt_statusline_draw -s
		prompt_statusline_segments -nr $_prompt_statusline_right_segments
	fi
}

#
# Prezto setup functions
#

function prompt_statusline_precmd_async {
	# Get Git repository information.
	if (( $+functions[git-info] )); then
		git-info
		typeset -p git_info >! $_prompt_statusline_precmd_async_data
	fi

	# Signal completion to parent process.
	kill -WINCH $$
}

function prompt_statusline_git_info {
	# Append Git status.
	if (( _prompt_statusline_precmd_async_pid > 0 )); then
		alias typeset='typeset -g'
		source $_prompt_statusline_precmd_async_data
		unalias typeset
		# Reset PID.
		_prompt_statusline_precmd_async_pid=0

		# Display prompt.
		zle && zle reset-prompt
	fi
}

function prompt_statusline_precmd {
	setopt LOCAL_OPTIONS
	unsetopt XTRACE KSH_ARRAYS

	# Check previous task runtime
	prompt_statusline_elapsed_time

	# Kill the old process of slow commands if it is still running.
	if (( _prompt_statusline_precmd_async_pid > 0 )); then
		kill -KILL $_prompt_statusline_precmd_async_pid &>/dev/null
	fi

	# Split off async task to check git status if in git repo
	if $(git rev-parse --is-inside-work-tree 2> /dev/null); then
		_prompt_statusline_git=1
		# Compute slow commands in the background.
		trap prompt_statusline_git_info WINCH
		prompt_statusline_precmd_async &!
		_prompt_statusline_precmd_async_pid=$!
	else
		unset _prompt_statusline_git
	fi
}

function prompt_statusline_preexec {
	_prompt_statusline_start_time=$SECONDS
}

function prompt_statusline_help {
	print -P \
"This prompt supports solarized light and dark. Solarized light with a white statusbar on two lines is the default style.
You can invoke it thus:

	prompt statusline [--color <color>] [--dark] [--single]

Set this theme with the following in your rc file:

	zstyle ':prezto:module:prompt' theme 'statusline' [--color <color>] [--dark] [--single]

%BOPTIONS%b
	-c, --color <color>
		Set the statusbar background color

	-d, --dark
		Use the solarized dark theme

	-s, --single
		Fit the prompt on a single line"
}

function prompt_statusline_preview {
	# Prevent changes from affecting default prompt
	local +h PROMPT=''
	local +h RPROMPT=''
	local +h PS2=''
	local +h RPS2=''
	local +h PS3=''
	local +h PS4=''
	local +h SPROMPT=''

	editor-info 2> /dev/null

	if (( # > 0 )); then
		prompt_preview_theme statusline "$@"
	else
		prompt_preview_theme statusline light
		print "$FX[none]"
		prompt_preview_theme statusline dark
	fi
}

# %s = undo standout, %u = remove underline, %b = unbold, %f = default foreground, %k = default background
# %S = standout, %U = underline, %B = bold, %F = fg (text) color, %K = bg color, %E = clear to end of line
function prompt_statusline_setup {
	setopt LOCAL_OPTIONS
	unsetopt XTRACE KSH_ARRAYS
	prompt_opts=(cr percent subst)
	_prompt_statusline_precmd_async_pid=0
	_prompt_statusline_precmd_async_data="${TMPPREFIX}-prompt_statusline_data"

	# Load required functions
	autoload -Uz add-zsh-hook

	# Add hooks
	add-zsh-hook preexec prompt_statusline_preexec
	add-zsh-hook precmd prompt_statusline_precmd

	# Completion styling
	zstyle ':completion:*:corrections' format "$(prompt_statusline_draw -a 2 7 '%d (errors: %e)')"
	zstyle ':completion:*:descriptions' format "$(prompt_statusline_draw -a 3 7 '%d')"
	zstyle ':completion:*:messages' format ' %F{13} -- %d --%f'
	zstyle ':completion:*:warnings' format "$(prompt_statusline_draw -cd 1 $_prompt_statusline_bg[1] 'No matches found')"
	zstyle ':completion:*:default' list-prompt '%S%M matches%s'
	zstyle ':completion:*' format "$(prompt_statusline_draw -a 3 7 '%d')"

	# Editor styling
	zstyle ':prezto:module:editor:info:completing' format '%B%F{1}...%f%b'
	zstyle ':prezto:module:editor:info:keymap:primary' format \
		"%F{4}%K{$_prompt_statusline_bg[1]}❯%K{$_prompt_statusline_bg[2]}%F{$_prompt_statusline_bg[1]}%f%k"
	zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{1}♺%f'
	zstyle ':prezto:module:editor:info:keymap:alternate' format \
		"%F{9}%K{$_prompt_statusline_bg[1]}❯%K{$_prompt_statusline_bg[2]}%F{$_prompt_statusline_bg[1]}%f%k"

	# Git-info styling
	zstyle ':prezto:module:git:info' verbose 'yes'
	zstyle ':prezto:module:git:info:action' format '%s'
	zstyle ':prezto:module:git:info:added' format '%F{2}✚%f'
	zstyle ':prezto:module:git:info:ahead' format '%F{3}↑%A%f'
	zstyle ':prezto:module:git:info:behind' format '%F{3}↓%B%f'
	zstyle ':prezto:module:git:info:branch' format ' %b'
	zstyle ':prezto:module:git:info:commit' format '%.7c'
	zstyle ':prezto:module:git:info:deleted' format '%F{1}✖%f'
	zstyle ':prezto:module:git:info:modified' format '3'
	zstyle ':prezto:module:git:info:position' format ':%p'
	zstyle ':prezto:module:git:info:renamed' format '%F{9}➜%f'
	zstyle ':prezto:module:git:info:stashed' format '%F{6}§%f'
	zstyle ':prezto:module:git:info:unmerged' format '%F{3}₥%f'
	zstyle ':prezto:module:git:info:untracked' format '1'
	zstyle ':prezto:module:git:info:keys' format \
		'prompt' '$(coalesce "%b" "%p" "%c")%s' \
		'rprompt' '%A%B%S%a%d%r%U' \
		'bg' '$(coalesce "%u" "%m" "2")' \
		'untracked' '%u'

	# Customize theme
	local -a dark color single
	zparseopts -D d=dark -dark=dark \
		c=color -color=color \
		s=single -single=single

	if [[ -n $dark ]]; then
		_prompt_statusline_bg=(0 8 0)
	fi

	# Set single-line prompt
	if [[ -n $single ]]; then
		[[ -n $color ]] && print -P '%F{1}%UWarning%u%f: cannot set color (-c) when in single-line mode (-s)' >&2
		_prompt_statusline_bg[3]="$_prompt_statusline_bg[2]"
		_prompt_statusline_left_segments=(status user git-branch directory)
		_prompt_statusline_right_segments=(git-status machine)
		PROMPT="%{$(print $FX[none])%}"'
$(prompt_statusline -sl)'"%{$(print $FX[none])%}"
		RPROMPT="%{$terminfo[cuf1]%}"'$(prompt_statusline -sr)'"%{$(print $FX[none])$terminfo[cuf1]%}"

	else
		PROMPT="%{$(print $FX[none])%}"'
$(prompt_statusline -l)
$editor_info[keymap]'
		RPROMPT='%{$terminfo[cuu1]$terminfo[cuf1]%}$(prompt_statusline -r)%{$(echotc DO 1)%}'

		if [[ -n $color ]]; then
			_prompt_statusline_bg[3]=$1
		fi
	fi

	# Secondary prompt, printed when the shell needs more information to complete a command
	PS2="%F{6}%K{$_prompt_statusline_bg[1]}❯%k%F{$_prompt_statusline_bg[1]}$_prompt_statusline_symbols[4]%f"
	RPS2="%k%F{$_prompt_statusline_bg[1]}$_prompt_statusline_symbols[1]%F{6}%K{$_prompt_statusline_bg[1]} %_%E"

	# Selection prompt used within a select loop
	PS3="$(prompt_statusline_draw -a 7 13 'Select:')"

	# Execution trace prompt
	# setopt xtrace # disable with unsetopt xtrace
	PS4="$(prompt_statusline_draw -a '' 2 '%N%f:%F{4}%i')"

	# Spelling prompt used to correct spelling errors
	SPROMPT="$(prompt_statusline_draw -a 9 7 'Correction:')change %B%F{1}%R%b%f to %B%F{2}%r%b%f [%Unyae%u]? "
}

prompt_statusline_setup "$@"
# vim: set ft=sh